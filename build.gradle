/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2014 the original author or authors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

buildscript {
	repositories {
		maven { url 'http://repo.springsource.org/plugins-release' }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'propdeps'

	group = 'io.jdev.pdf'
	version = '0.2-SNAPSHOT'

	repositories {
		mavenCentral()
	}

	ext {
		groovyVersion = '1.8.6'
		itextVersion = '5.5.2'
		gebVersion = '0.9.3'
		seleniumVersion = '2.42.2'

		isSnapshot = version.endsWith("-SNAPSHOT")
	}

	dependencies {
		// someone using this library will already have their own version
		// of groovy around, so don't add it as a pom dependency
		provided "org.codehaus.groovy:groovy-all:$groovyVersion"

		testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
		testRuntime "cglib:cglib-nodep:3.1"
	}

	targetCompatibility = 1.6
	sourceCompatibility = 1.6

	test {
		testLogging {
			exceptionFormat = 'full'
		}
	}
}

allprojects {
	apply plugin: 'idea'

	task resolveAllDependencies {
		doLast {
			configurations.all { it.resolve() }
		}
	}
}

idea {
	project {
		jdkName "1.6"
		ipr {
			withXml { provider ->
				def node = provider.asNode()
				def copyrightManager = node.component.find { it.'@name' == 'CopyrightManager' }
				copyrightManager.@default = "MIT"
				def aslCopyright = copyrightManager.copyright.find { it.option.find { it.@name == "myName" }?.@value == "MIT" }
				if (aslCopyright == null) {
					copyrightManager.append(new XmlParser().parseText('''
						<copyright>
							<option name="notice" value="The MIT License (MIT)&#10;&#10;Copyright (c) &amp;#36;today.year the original author or authors&#10;&#10;Permission is hereby granted, free of charge, to any person obtaining a copy&#10;of this software and associated documentation files (the &quot;Software&quot;), to deal&#10;in the Software without restriction, including without limitation the rights&#10;to use, copy, modify, merge, publish, distribute, sublicense, and/or sell&#10;copies of the Software, and to permit persons to whom the Software is&#10;furnished to do so, subject to the following conditions:&#10;&#10;The above copyright notice and this permission notice shall be included in&#10;all copies or substantial portions of the Software.&#10;&#10;THE SOFTWARE IS PROVIDED &quot;AS IS&quot;, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR&#10;IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,&#10;FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE&#10;AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER&#10;LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,&#10;OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN&#10;THE SOFTWARE." />
							<option name="keyword" value="Copyright" />
							<option name="allowReplaceKeyword" value="" />
							<option name="myName" value="MIT" />
							<option name="myLocal" value="true" />
						</copyright>
					'''))
				}
			}
		}
	}
}
